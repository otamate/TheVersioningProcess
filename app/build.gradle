apply plugin: 'com.android.application'

import org.tmatesoft.svn.core.wc.*

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.otamate.android.theversionprocess"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ext.CMRevNumber = getSvnRevision()
        ext.CIBuildNumber = System.getenv("BUILD_NUMBER") ?: "dev"
        versionName "Beta-1.1.0-$CMRevNumber-$CIBuildNumber"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            sourceSets.debug.resources.srcDirs = ['src/debug/res']
            buildConfigField 'Boolean', 'BUILD_EXPIRES', 'true'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            buildConfigField 'Boolean', 'BUILD_EXPIRES', 'false'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = getAppNameFromResource() + "-${variant.name}-${variant.versionName}.apk"
        }
    }
}

def getSvnRevision(){
    ISVNOptions options = SVNWCUtil.createDefaultOptions(true)
    SVNClientManager clientManager = SVNClientManager.newInstance(options)
    SVNStatusClient statusClient = clientManager.getStatusClient()
    SVNStatus status = statusClient.doStatus(projectDir, false)
    SVNRevision revision = status.getRevision()
    return revision.getNumber()
}

def getAppNameFromResource() {
    def stringResources = android.sourceSets.main.res.sourceFiles.find {
        it.name in ['strings.xml']
    }

    XmlParser parser = new XmlParser()
    String appName = parser.parse(stringResources).string.find {
        it.@name in ['app_name']
    }.text()

    return appName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
